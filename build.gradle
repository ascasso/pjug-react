plugins {
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'com.github.node-gradle.node' version '7.1.0'
}

group = 'org.pjug'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = '21'

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    runtimeOnly('org.postgresql:postgresql')
    implementation('org.flywaydb:flyway-core')
    implementation('org.flywaydb:flyway-database-postgresql')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('io.github.wimdeblauwe:error-handling-spring-boot-starter:4.5.0')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3')
    implementation('org.mapstruct:mapstruct:1.6.3')
    annotationProcessor('org.mapstruct:mapstruct-processor:1.6.3')
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    developmentOnly('org.springframework.boot:spring-boot-docker-compose')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('io.rest-assured:rest-assured')
    testImplementation('org.springframework.boot:spring-boot-testcontainers')
    testImplementation('org.testcontainers:postgresql')
}

node {
    download.set(true)
    version.set('22.13.1')
}

task npmRunBuild(type: NpmTask) {
    args = ['run', 'build']
    dependsOn npmInstall

    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('src/main/webapp'))
    inputs.file('.env')
    inputs.file('package.json')
    inputs.file('tsconfig.json')
    inputs.file('webpack.config.js')
    outputs.dir(layout.buildDirectory.dir('resources/main/static'))
}

processResources {
    dependsOn npmRunBuild
}

tasks.withType(Test) {
    useJUnitPlatform()
}
